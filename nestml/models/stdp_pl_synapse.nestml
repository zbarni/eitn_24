"""
stdp_pl - Synapse model for spike-timing dependent plasticity with power law weight dependence
###############################################################################################

Description
+++++++++++

stdp_pl_synapse is a synapse with spike time dependent plasticity as defined in [1]_). 

References
++++++++++

.. [1] Morrison A., Aertsen A., Diesmann M. (2007). Spike-timing-dependent plasticity in balanced random networks. Neural Computation 19(6):1437-1467

"""

synapse stdp_pl:

  state:
    w           real = 1.    @nest::weight  # synaptic weight
    pre_trace   real = 0.                   # presynaptic trace  
    post_trace  real = 0.                   # postsynaptic trace

  parameters:
    the_delay   ms   =  1    ms  @nest::delay 
    lambda      real =  0.01
    alpha       real =  0.1
    tau_tr_pre  ms   = 15    ms
    tau_tr_post ms   = 30    ms
    mu_plus     real =  0.4
    w_0         real =  1.0      # reference weights
                                 # note: sign(w_0)==sign(w)
  equations:
    pre_trace'  = - pre_trace / tau_tr_pre
    post_trace' = - post_trace / tau_tr_post    

  input:
    pre_spikes  <- spike
    post_spikes <- spike

  output:
    spike

  onReceive(post_spikes):
    post_trace += 1
    
    # update for causal firing (potentiation)
    w = w + lambda * w_0 * (w/w_0)**mu_plus * pre_trace

  onReceive(pre_spikes):
    pre_trace += 1
    
    # update for acausal firing (depression)
    w_ real = w/w_0 - alpha*lambda * w/w_0 * post_trace

    # zero clipping
    w = max(0.0,w_) * w_0      
    
    # deliver spike to postsynaptic partner
    deliver_spike(w, the_delay)

